/*
Fuzzy Logic Script
@author Markus
*/

FUNCTION_BLOCK hunger       // How hungry are you?

// input variables (Physical activity and Last time you ate food)
VAR_INPUT
         activity : real;
         time : real;
END_VAR

// output variable (Hunger)
VAR_OUTPUT
        hunger : REAL;
END_VAR

// Fuzzify Physical activity input variable
FUZZIFY activity
        TERM light := (0,1) (6,0);
        TERM heavy := (4,0) (10,1);
END_FUZZIFY

// Fuzzify Last time you ate food variable
FUZZIFY time
        TERM recently := (0,1) (4,0);
        TERM while := (3,0) (5,1) (7,0);
        TERM long := (6,0) (10,1);
END_FUZZIFY

// Defuzzify Hunger output variable
DEFUZZIFY hunger
        TERM full := (0,0) (5,1) (10,0);
        TERM peckish := (10,0) (15,1) (20,0);
        TERM starving := (20,0) (25,1) (30,0);
        METHOD : COG;                            // "Center Of Gravity" defuzzification method 
        DEFAULT := 0                             // Default value will be zero if no rule activates the defuzzifier
END_DEFUZZIFY

// Inference Rules
RULEBLOCK No1
	    AND : MIN;	// uses "min" for "and"
	    ACT : MIN;	// uses "min" activation method
	    ACCU : MAX;	// uses "max" accumulation method

        RULE 1 : IF time IS long OR activity IS heavy THEN hunger IS starving;
        RULE 2 : IF time IS while THEN hunger IS peckish;
        RULE 3 : IF time IS recently THEN hunger IS full;
END_RULEBLOCK

END_FUNCTION_BLOCK